<PROGRAMA> ::= {<DECLARACAO-DE-EXPRESSAO>} {<DEFINICAO-DE-FUNCAO>} <PRINCIPAL>

<DEFINICAO-DE-FUNCAO> ::= <ESPECIFICADOR-DE-TIPO-DE-FUNCAO> <IDENTIFICADOR> "(" [<LISTA-DE-PARAMETROS>] ")" "{" <LISTA-DE-DECLARACAO> "}"

<PRINCIPAL> ::= "INTEIRO" "PRINCIPAL" "(" ")" "{" <LISTA-DE-DECLARACAO> "}"

<ESPECIFICADOR-DE-TIPO-DE-FUNCAO> ::= "INTEIRO" | "FLUTUANTE" | "CARACTERE" | "SIMOUNAO" | "VAZIO"

<ESPECIFICADOR-DE-TIPO> ::= "INTEIRO" | "FLUTUANTE" | "CARACTERE" | "SIMOUNAO"

<LISTA-DE-PARAMETROS> ::= <PARAMETRO> "," <LISTA-DE-PARAMETROS> | <PARAMETRO> 

<PARAMETRO> ::= <ESPECIFICADOR-DE-TIPO> <IDENTIFICADOR>

<DECLARACAO-COMPOSTA> ::= "{" <LISTA-DE-DECLARACAO> "}"

<LISTA-DE-DECLARACAO> ::= <DECLARACAO> <LISTA-DE-DECLARACAO> | <DECLARACAO>

<DECLARACAO> ::= <DECLARACAO-DE-EXPRESSAO> | <DECLARACAO-COMPOSTA> | <DECLARACAO-DE-SELECAO> | <DECLARACAO-DE-ITERACAO> | <DECLARACAO-DE-RETORNO> | <DECLARACAO-DE-REGRA-DE-TRES>

<DECLARACAO-DE-EXPRESSAO> ::= [ <ESPECIFICADOR-DE-TIPO> ] <EXPRESSAO-DE-ATRIBUICAO> ";" 

<DECLARACAO-DE-SELECAO> ::= "SE" "(" <EXPRESSAO-DE-ATRIBUICAO> ")" <DECLARACAO-COMPOSTA> [ "SENAO" <DECLARACAO-COMPOSTA> ]

<DECLARACAO-DE-ITERACAO> ::= "ENQUANTO" "(" <EXPRESSAO-DE-ATRIBUICAO> ")" <DECLARACAO-COMPOSTA> | "PARA" "(" <DECLARACAO-DE-EXPRESSAO> <EXPRESSAO-DE-ATRIBUICAO> ";" <EXPRESSAO-DE-ATRIBUICAO> ")" <DECLARACAO-COMPOSTA>

<DECLARACAO-DE-RETORNO> ::= "RETORNA" [ <EXPRESSAO-DE-ATRIBUICAO> ] ";"

<DECLARACAO-DE-REGRA-DE-TRES> ::= "REGRADETRES" "(" <EXPRESSAO-PRIMARIA> "," <EXPRESSAO-PRIMARIA> "," <EXPRESSAO-PRIMARIA> ")" ";"

<EXPRESSAO-DE-ATRIBUICAO> ::= <EXPRESSAO-LOGICA-OU> | <EXPRESSAO-POSFIXA> "IGUAL" <EXPRESSAO-DE-ATRIBUICAO>

<EXPRESSAO-LOGICA-OU> ::= <EXPRESSAO-LOGICA-E> | <EXPRESSAO-LOGICA-E> "OU" <EXPRESSAO-LOGICA-OU>

<EXPRESSAO-LOGICA-E> ::= <EXPRESSAO-DE-IGUALDADE> | <EXPRESSAO-DE-IGUALDADE> "E" <EXPRESSAO-LOGICA-E>

<EXPRESSAO-DE-IGUALDADE> ::= <EXPRESSAO-RELACIONAL> | <EXPRESSAO-RELACIONAL> "COMPARADO" <EXPRESSAO-DE-IGUALDADE> | <EXPRESSAO-RELACIONAL> "DIFERENTE" <EXPRESSAO-DE-IGUALDADE>

<EXPRESSAO-RELACIONAL> ::= <EXPRESSAO-ADITIVA> | <EXPRESSAO-ADITIVA> "MENOR" <EXPRESSAO-RELACIONAL> | <EXPRESSAO-ADITIVA> "MAIOR" <EXPRESSAO-RELACIONAL> | <EXPRESSAO-ADITIVA> "MENOROUIGUAL" <EXPRESSAO-RELACIONAL> | <EXPRESSAO-ADITIVA> "MAIOROUIGUAL" <EXPRESSAO-RELACIONAL>

<EXPRESSAO-ADITIVA> ::= <EXPRESSAO-MULTIPLICATIVA> | <EXPRESSAO-MULTIPLICATIVA> "MAIS" <EXPRESSAO-ADITIVA> | <EXPRESSAO-MULTIPLICATIVA> "MENOS" <EXPRESSAO-ADITIVA>

<EXPRESSAO-MULTIPLICATIVA> ::= <EXPRESSAO-POSFIXA> | <EXPRESSAO-POSFIXA> "VEZES" <EXPRESSAO-MULTIPLICATIVA> | <EXPRESSAO-POSFIXA> "DIVIDIDO" <EXPRESSAO-MULTIPLICATIVA>

<EXPRESSAO-POSFIXA> ::= <EXPRESSAO-PRIMARIA> | <EXPRESSAO-POSFIXA> "[" <EXPRESSAO-DE-ATRIBUICAO> "]" | <EXPRESSAO-POSFIXA> "(" [ <LISTA-DE-EXPRESSAO-DE-ARGUMENTO> ] ")" | <EXPRESSAO-POSFIXA> "." <IDENTIFICADOR>

<LISTA-DE-EXPRESSAO-DE-ARGUMENTO> ::= <EXPRESSAO-DE-ATRIBUICAO> | <EXPRESSAO-DE-ATRIBUICAO> "," <LISTA-DE-EXPRESSAO-DE-ARGUMENTO>

<EXPRESSAO-PRIMARIA> ::= <IDENTIFICADOR> | <CONSTANTE> | "(" <EXPRESSAO-DE-ATRIBUICAO> ")"

<CONSTANTE> ::= <INTEIRO> | <FLUTUANTE> | <CARACTERE>

<IDENTIFICADOR> ::= <LETRA> { <LETRA> | <DIGITO> }

<INTEIRO> ::= <DIGITO> { <DIGITO> }

<FLUTUANTE> ::= <DIGITO> "." <DIGITO> { <DIGITO> }

<CARACTERE> ::= "'" <SIMBOLO> "'"

<SIMBOLO> ::= <LETRA> | <DIGITO> | "(" | ")" | "{" | "}" | "[" | "]"

<LETRA> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

<DIGITO> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" 

